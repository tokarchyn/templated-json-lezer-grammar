@precedence { objectTemplate @left, stringOrCB @left }

@top JsonText { value }

value { True | False | Null | Number | String | Object | Array | Template }

String { string }
Object { "{" list<Property>? "}" }
Array  { "[" list<value>? "]" }

Property { PropertyName ":" value }
PropertyName { string }

Template { openTemplate !stringOrCB stringOrCB closeTemplate }
stringOrCB { charNonCBs* !objectTemplate objectTemplate? charNonCBs* }
objectTemplate { "{" !stringOrCB stringOrCB? "}" }

@tokens {
  True  { "true" }
  False { "false" }
  Null  { "null" }

  Number { '-'? int frac? exp?  }
  int  { '0' | $[1-9] @digit* }
  frac { '.' @digit+ }
  exp  { $[eE] $[+\-]? @digit+ }

  string { '"' char* '"' }
  char { $[\u{20}\u{21}\u{23}-\u{5b}\u{5d}-\u{10ffff}] | "\\" esc }
  esc  { $["\\\/bfnrt] | "u" hex hex hex hex }
  hex  { $[0-9a-fA-F] }

  openTemplate { "${" }
  closeTemplate { "}" }
  charNonCBs { $["\u{21}\u{23}-\u{5b}\u{5d}-\u{7a}\u{7c}\u{7e}-\u{10ffff}] | "\\" esc }

  whitespace { $[ \n\r\t] }

  "{" "}" "[" "]" 
}

@skip { whitespace }
list<item> { item ("," item)* }

@external propSource jsonHighlighting from "./highlight"

@detectDelim
